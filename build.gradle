buildscript {
    ext {
        jooqVersion = '3.19.5'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'nu.studer.jooq' version '9.0'
    id 'jacoco'
}

allprojects {

    group = 'sites.offload'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    jacocoTestReport {
        reports {
            html.required = true
            xml.required = true
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.8
                }

                excludes = [
                        '*.*Exception',
                        '**/dto/request/**',
                        '**/dto/response/**',
                        '**/exception/**',
                ]
            }
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy('jacocoTestReport')
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/generated", "offroad-db/db-jpa/src/generated"]
        }
    }
}


dependencies {
    implementation "org.jooq:jooq:${jooqVersion}"

    jooqGenerator project(':offroad-db:db-jooq')
    jooqGenerator "org.jooq:jooq:${jooqVersion}"
    jooqGenerator "org.jooq:jooq-meta:${jooqVersion}"

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation ('org.springframework.boot:spring-boot-starter-jooq') {
        exclude group: 'org.jooq'
    }


    // codegen 시점에 entity 포함
    jooqGenerator project(':offroad-db:db-jpa')

    // 만약 application 모듈에서 엔티티 모듈을 사용하고 싶다면 주석 해제
    // implementation project(':entity')

    jooqGenerator "org.jooq:jooq-meta-extensions-hibernate:${jooqVersion}"

    // in-memory H2로 JOOQ dsl 생성을 위함
    // h2의 v2 버전 호환성 때문에 상위버전 대신 1.4.200 버전을 추천
    jooqGenerator 'com.h2database:h2:1.4.200'
}

tasks.named('test') {
    useJUnitPlatform()
}

jooq {
    version = "${jooqVersion}"
    configurations {
        prodDB {
            generationTool {
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'

                    database {
                        name = 'org.jooq.meta.extensions.jpa.JPADatabase'
                        unsignedTypes = true

                        properties {
                            property {
                                key = 'packages'
                                value = 'site.offload.dbjpa'
                            }

                            // DSL 생성시, jpa의 AttributeConverters 에 따라 jooq DSL의 타입을 매핑할지 여부
                            property {
                                key = 'useAttributeConverters'
                                value = true
                            }
                        }

                        forcedTypes {
                            forcedType {
                                userType = 'java.lang.Long'
                                includeTypes = 'int unsigned'
                            }

                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'tinyint unsigned'
                            }

                            forcedType {
                                userType = 'java.lang.Integer'
                                includeTypes = 'smallint unsigned'
                            }
                        }
                    }

                    generate {
                        daos = true
                        records = true
                        fluentSetters = true
                        javaTimeTypes = true
                        deprecated = false
                    }

                    target {
                        directory = 'offroad-db/db-jpa/src/generated'
                    }

                    // jooq-custom 내부의 설정
                    strategy.name = 'site.offload.dbjooq.generator.JPrefixGeneratorStrategy'
                }
            }
        }
    }
}
